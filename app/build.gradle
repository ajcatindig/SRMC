plugins {
    id 'com.google.devtools.ksp'
}
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'kover'

android {
    compileSdkVersion ProjectConfig.compileSdkVersion

    defaultConfig {
        minSdkVersion ProjectConfig.minSdkVersion
        targetSdkVersion ProjectConfig.targetSdkVersion
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions {
        jvmTarget = javaVersion
    }

    kotlin {
        jvmToolchain(javaVersion.toInteger())
    }

    // Include sources generated by Mutekt
    libraryVariants.all { variant ->
        kotlin.sourceSets {
            def name = variant.name
            getByName(name) {
                kotlin.srcDir("build/generated/ksp/$name/kotlin")
            }
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            useJUnitPlatform()
        }
    }
    lint {
        abortOnError false
    }

    namespace 'com.example.srmc'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = javaVersion
    }
}

dependencies {

    // Core Module
    implementation project(":core")

    // Repository Module
    api project(":repository")

    // Android
    implementation "androidx.core:core-ktx:$coreKtxVersion"
    implementation "androidx.legacy:legacy-support-v4:$legacySupportVersion"

    // Material Design
    implementation "com.google.android.material:material:$materialDesignVersion"

    // Lifecycle
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    api "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:$workmanagerVersion"
    implementation "androidx.work:work-multiprocess:$workmanagerVersion"

    // Jetpack DataStore
    implementation "androidx.datastore:datastore-preferences:$dataStoreVersion"

    // JetPack Security
    implementation "androidx.security:security-crypto:$securityCryptoVersion"

    // Hilt + JetPack
    api "androidx.hilt:hilt-work:$jetpackHiltVersion"
    kapt "androidx.hilt:hilt-compiler:$jetpackHiltVersion"

    // Dagger + Hilt
    implementation "com.google.dagger:hilt-android:$daggerHiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$daggerHiltVersion"

    // Mutekt
    implementation("dev.shreyaspatil.mutekt:mutekt-core:$mutektVersion")
    ksp("dev.shreyaspatil.mutekt:mutekt-codegen:$mutektVersion")

    // Compose runtime - For @Immutable annotation
    implementation platform("androidx.compose:compose-bom:$composeBomVersion")
    testImplementation platform("androidx.compose:compose-bom:$composeBomVersion")
    implementation "androidx.compose.runtime:runtime"

    // Testing
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "io.kotest:kotest-runner-junit5:$kotestVersion"
    testImplementation "io.kotest:kotest-assertions-core:$kotestVersion"
    testImplementation "io.kotest:kotest-property:$kotestVersion"
    testImplementation "io.mockk:mockk:$mockkVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
}
